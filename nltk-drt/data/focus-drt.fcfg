% start S
############################
# Grammar Rules
#############################

#S[SEM = <?subj(?vp)>] -> NP[NUM=?n,SEM=?subj] VP[NUM=?n,SEM=?vp]

S[SEM = <FOCUS(AGENT,?subj,[?n,?g])(?vp)>] -> NP[NUM=?n,GEN=?g,SEM=?subj] VP[NUM=?n,SEM=?vp]

NP[NUM=?n,GEN=?g,SEM=<?det(?nom)> ] -> Det[NUM=?n,SEM=?det]  Nom[NUM=?n,GEN=?g,SEM=?nom]
NP[NUM=?n,GEN=?g,SEM=?np] -> PropN[NUM=?n,GEN=?g,SEM=?np]

Nom[NUM=?n,GEN=?g,SEM=?nom] -> N[NUM=?n,GEN=?g,SEM=?nom]

VP[NUM=?n,SEM=<?v(FOCUS(THEME,?obj,[?n,?g]))>] -> TV[NUM=?n,SEM=?v] NP[NUM=?n,GEN=?g,SEM=?obj]
#VP[NUM=?n,SEM=<?v(?obj)>] -> TV[NUM=?n,SEM=?v] NP[NUM=?n,GEN=?g,SEM=?obj]
TV[NUM=?n,SEM=<?v(?obj)>] -> DV[NUM=?n,SEM=?v] NP[SEM=?obj]
VP[NUM=?n,SEM=<?v>] -> IV[NUM=?n,SEM=?v]

#############################
# Lexical Rules
#############################

PropN[NUM=sg,GEN=m,SEM=<\P.(DRS([x],[Butch(x)])+P(x))>] -> 'Butch'
PropN[NUM=sg,GEN=f,SEM=<\P.(DRS([x],[Mia(x)])+P(x))>] -> 'Mia'
PropN[NUM=sg,GEN=m,SEM=<\P.(DRS([x],[PRO(x)])+P(x))>] -> 'he' | 'He'
PropN[NUM=sg,GEN=f,SEM=<\P.(DRS([x],[PRO(x)])+P(x))>] -> 'she' | 'She'
PropN[NUM=sg,GEN=n,SEM=<\P.(DRS([x],[PRO(x)])+P(x))>] -> 'it' | 'It'

Det[NUM=sg,SEM=<\P Q.((DRS([x],[])+P(x))+Q(x))>] -> 'a' | 'A'
Det[NUM=sg,SEM=<\P Q.DRS([],[-((DRS([x],[])+P(x))+Q(x))])>] -> 'no' | 'No'

N[NUM=sg,GEN=n,SEM=<\x.DRS([],[hammer(x)])>] -> 'hammer'
N[NUM=sg,GEN=n,SEM=<\x.DRS([],[chainsaw(x)])>] -> 'chainsaw'

TV[+V,NUM=sg,SEM=<\X x.X(\y.DRS([],[pick_up(x,y)]))>,tns=pres] -> 'picks-up'
TV[+V,NUM=sg,SEM=<\X x.X(\y.DRS([],[discard(x,y)]))>,tns=pres] -> 'discards'
TV[+V,NUM=sg,SEM=<\X x.X(\y.DRS([],[like(x,y)]))>,tns=pres] -> 'likes'
DV[+V,NUM=sg,SEM=<\X Y x.Y(\z.X(\y.DRS([],[give(x,y,z)])))>,tns=pres] -> 'gives'
IV[+V,NUM=sg,SEM=<\x.DRS([],[smoke(x)])>,tns=pres] -> 'smokes'

